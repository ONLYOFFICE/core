apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {

    buildToolsVersion '30.0.2'
    compileSdkVersion 30

    defaultConfig {

        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName '1.0'

        buildConfigField "String", "LIB_X2T", "\"$NAME_LIB\""

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_TOOLCHAIN=clang",
                          "-DANDROID_STL=c++_static",
                          "-DANDROID_ARM_NEON=TRUE",
                          "-DARG_PATH_LIB_BUILD_TOOLS=${getProjectPath(PATH_LIB_BUILD_TOOLS)}",
                          "-DARG_PATH_LIB_DST=${getProjectPath(PATH_LIB_DST, true)}",
                          "-DARG_PATH_SRC_CORE=${getProjectPath(PATH_SRC_CORE)}",
                          "-DARG_NAME_LIB=$NAME_LIB"
            }
        }

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
            jniDebuggable true
        }
    }

    sourceSets {
        main {
            java.srcDirs = [
                "src/main/java"
            ]

            jni.srcDirs = [
                "src/main/cpp"
            ]

            jniLibs.srcDirs = [
                PATH_LIB_DST,
                PATH_LIB_BUILD_TOOLS
            ]
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        ['armeabi-v7a', 'x86' ,'arm64-v8a',  'x86_64' ].each { abi ->
            def dh = file("$PATH_LIB_BUILD_TOOLS/$abi")
            dh.eachFile {
                if(it.getName().contains(".so"))
                    pickFirst "lib/$abi/${it.getName()}"
            }
            pickFirst "lib/$abi/lib${NAME_LIB}.so"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}



/*
* Check the path for existence,
* otherwise get the path relative to the current project,
* otherwise the error is in the path
* */
def getProjectPath(String path, Boolean isRelativeCreate = false) {
    def absolutePath = file(path)
    def relativePath = file("${file('.').absolutePath}/$path")
    //def relativePath = file("${rootProject.projectDir}/path")

    if (absolutePath.exists() && absolutePath.isDirectory()) {
        return absolutePath.toString().replaceAll("\\\\", "/")
    } else if ((relativePath.exists() && relativePath.isDirectory()) || (isRelativeCreate && relativePath.mkdirs())) {
        return relativePath.toString().replaceAll("\\\\", "/")
    }

    throw new GradleException("getProjectPath($path) - path doesn't exist...")
}

task copyIcuDatFiles {
    doLast {

        println ""
        println "------------------------- ICUDATFILES (task copyIcuDatFiles()) -------------------------"

        def pathAssets = "${file('.').absolutePath}/src/main/assets/icu"
        if (file(pathAssets).exists()) {
            println "IcuDatFiles already exist."
            println "Delete ${pathAssets} folder for update icuDatFiles."
            return
        }

        if (!project.hasProperty('PATH_SRC_ICU_DAT_FILE')) {
            throw new GradleException("Property PATH_SRC_ICU_DAT_FILE with core repository path doesn't exist...")
        }

        if (!project.hasProperty('PATH_SRC_ICU_V8_DAT_FILE')) {
            throw new GradleException("Property PATH_SRC_ICU_V8_DAT_FILE with core repository path doesn't exist...")
        }

        def pathIcuDatFiles = PATH_SRC_ICU_DAT_FILE

        def pathIcuV8DatFiles = PATH_SRC_ICU_V8_DAT_FILE

        if (!file(pathIcuDatFiles).exists()) {
            throw new GradleException("Path with core repository doesn't exist...")
        }

        if (!file(pathIcuV8DatFiles).exists()) {
            throw new GradleException("Path with core repository doesn't exist...")
        }

        copy {
            println "\nCopy dat files..."
            eachFile { println it.file }
            from pathIcuDatFiles, pathIcuV8DatFiles
            into pathAssets
        }
    }
}

preBuild.dependsOn copyIcuDatFiles